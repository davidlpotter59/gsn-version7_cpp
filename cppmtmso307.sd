%%
Screen
        Name            = "cppmtmso307" ;
        Type            = "Detail" ;
        Padcharacter    = Space ;
        Highlite        = Underscore ;
        Screensize      = "dynamic" ;
        Windowtitle = "%a     %s Business Income Off Premises Loss of Service Coverage    Ver 7.00 %m" ;
        Domain          = "CPPBUSOFFPREM" ;

        Global Unsigned Ascii Number G_POLICY_NO[9] ;
        Global Unsigned Ascii Number G_YEAR[4] ;
        Global Unsigned Ascii Number G_END_SEQUENCE[4] ;
        Global Unsigned Ascii Number G_PREM_NO[4] ;
        Global Unsigned Ascii Number G_BUILD_NO[4] ;

        Global String G_END_SCREEN[1] ;

        Local Unsigned Ascii Number L_SUB_CODE[4],
                                    L_STATE[2] ,
                                    l_line_of_business[4]=0008 ;

        Local String L_CODE[7],
                     L_CODE_1[7],
                     L_ADD_ENDORSEMENT[1] ,
                     L_FORM_EDITION[18],
                     L_DESCRIPTION[55] ;

        Access CPPBUSOFFPREM,
           Set CPPBUSOFFPREM:POLICY_NO = G_POLICY_NO ,
               CPPBUSOFFPREM:POL_YEAR = G_YEAR,
               CPPBUSOFFPREM:END_SEQUENCE = G_END_SEQUENCE,
               CPPBUSOFFPREM:PREM_NO = G_PREM_NO ,
               CPPBUSOFFPREM:BUILD_NO = G_BUILD_NO ;

        Include "STDKEYS7.INC"

style definition

        include "styles.inc"

Functions

        Include "STDFUNC7.INC"

menu definition

        include "stdmenu7.inc"

}

toolbar definition

        include "stdtoolbar7.inc"

}

Access Validation
If CPPBUSOFFPREM:POLICY_NO <> G_POLICY_NO Or
   CPPBUSOFFPREM:POL_YEAR <> G_YEAR Or
   CPPBUSOFFPREM:END_SEQUENCE <> G_END_SEQUENCE Or
   CPPBUSOFFPREM:PREM_NO <> G_PREM_NO Or
   CPPBUSOFFPREM:BUILD_NO <> G_BUILD_NO Then
    Error 1000 ;


screen at 1,1 to 20,80
Properties
        {
        Layouttype = column
        }
{
Panel
Properties
        {
        Borderstyle     = raised
        Scrollbarpolicy = asneeded
        }
{
%%
                                                        _______________
*------------------------------------------------------------------------------*
| Policy No:   _________  ____  ____                                           |
| Location No: ____                                                            |
| Building No: ____                                                            |
*------------------------------------------------------------------------------*
| CS:          _   (Communication Supplier)                                    |
| PS:          _   (Power Supplier)                                            |
| WS:          _   (Water Supplier)                                            |
| Cause Loss Description:                                                      |
| ___________________________________________________________________________  |
*------------------------------------------------------------------------------*
%%

components

Fields
101     FUNCTION/noecho/showasguidetext ;
301     Begin
        If FUNCTION = "ADD" Then
            Begin
            CPPBUSOFFPREM:POLICY_NO = G_POLICY_NO
            CPPBUSOFFPREM:POL_YEAR = G_YEAR
            CPPBUSOFFPREM:END_SEQUENCE = G_END_SEQUENCE
            CPPBUSOFFPREM:PREM_NO = G_PREM_NO
            CPPBUSOFFPREM:BUILD_NO = G_BUILD_NO
            End
        End
        CPPBUSOFFPREM:POLICY_NO/displayonly ;
302     CPPBUSOFFPREM:POL_YEAR/displayonly;
303     CPPBUSOFFPREM:END_SEQUENCE/displayonly;
401     CPPBUSOFFPREM:PREM_NO/displayonly ;
501     CPPBUSOFFPREM:BUILD_NO/displayonly ;
701     CPPBUSOFFPREM:CS/checkbox/values="0,1" ;
801     CPPBUSOFFPREM:PS/checkbox/values="0,1" ;
901     CPPBUSOFFPREM:WS/checkbox/values="0,1" ;
1101    CPPBUSOFFPREM:CAUSE_LOSS_DESCRIPTION;

}

}


Update
If UPDATEOK = "YES" And
   ABORT = "NO" And
   G_END_SCREEN  Not One Of "Y" Then
    Begin
    L_STATE = CPPGENERAL:STATE
    Access CPSBUSMISCRATES, Set CPSBUSMISCRATES:COMPANY_ID = SFPNAME:COMPANY_ID,
                                CPSBUSMISCRATES:STATE = L_STATE,
                                CPSBUSMISCRATES:LINE_OF_BUSINESS = l_line_of_business, Generic

    While CPSBUSMISCRATES:COMPANY_ID = SFPNAME:COMPANY_ID And
          CPSBUSMISCRATES:STATE = L_STATE And
          CPSBUSMISCRATES:LINE_OF_BUSINESS = l_line_of_business
            Begin
            If SFPNAME:EFF_DATE >= CPSBUSMISCRATES:EFF_DATE Then
                Begin
                L_CODE_1 = CPSBUSMISCRATES:OFF_PREM_CODE
                End
            Next CPSBUSMISCRATES
            End

    If L_CODE_1 <> "" Then
        Begin
        L_CODE = L_CODE_1
        Do ACCESS_SFPEND
        If L_ADD_ENDORSEMENT = "Y" Then
            Do ADD_SFPEND
        End

    End

Procedure Definition

Procedure ACCESS_SFPEND
Begin
Access SFPEND, Set SFPEND:POLICY_NO = G_POLICY_NO ,
                   SFPEND:POL_YEAR = G_YEAR,
                   SFPEND:END_SEQUENCE = G_END_SEQUENCE,
                   SFPEND:PREM_NO = G_PREM_NO,
                   SFPEND:BUILD_NO = G_BUILD_NO, Generic

L_ADD_ENDORSEMENT = "Y"
Repeat Begin
    Exit If SFPEND:POLICY_NO <> G_POLICY_NO Or
            SFPEND:POL_YEAR <> G_YEAR Or
            SFPEND:END_SEQUENCE <> G_END_SEQUENCE Or
            SFPEND:PREM_NO <> G_PREM_NO Or
            SFPEND:BUILD_NO <> G_BUILD_NO

    If SFPEND:CODE = L_CODE Then
        L_ADD_ENDORSEMENT = "N"

    Next SFPEND
    End

End

Procedure ADD_SFPEND
Begin
Access SFPEND, Set SFPEND:POLICY_NO = G_POLICY_NO ,
                   SFPEND:POL_YEAR = G_YEAR,
                   SFPEND:END_SEQUENCE = G_END_SEQUENCE,
                   SFPEND:PREM_NO = G_PREM_NO,
                   SFPEND:BUILD_NO = G_BUILD_NO, Generic

L_SUB_CODE = 0
Repeat Begin
    Exit If SFPEND:POLICY_NO <> G_POLICY_NO Or
            SFPEND:POL_YEAR <> G_YEAR Or
            SFPEND:END_SEQUENCE <> G_END_SEQUENCE Or
            SFPEND:PREM_NO <> G_PREM_NO Or
            SFPEND:BUILD_NO <> G_BUILD_NO

    L_SUB_CODE = SFPEND:SUB_CODE

    Next SFPEND
    End

L_SUB_CODE = L_SUB_CODE + 10

Access CPPGENERAL_ALIAS, Set CPPGENERAL_ALIAS:POLICY_NO = G_POLICY_NO,
                             CPPGENERAL_ALIAS:POL_YEAR = G_YEAR,
                             CPPGENERAL_ALIAS:END_SEQUENCE = G_END_SEQUENCE,
                             CPPGENERAL_ALIAS:PREM_NO = G_PREM_NO,
                             CPPGENERAL_ALIAS:BUILD_NO = G_BUILD_NO, Generic

L_STATE = CPPGENERAL_ALIAS:STATE
Access SFSOPTEND, Set SFSOPTEND:COMPANY_ID = SFPNAME:COMPANY_ID,
                      SFSOPTEND:STATE = L_STATE,
                      SFSOPTEND:LINE_OF_BUSINESS = l_line_of_business,
                      SFSOPTEND:CODE = L_CODE, Generic

Repeat Begin
    Exit If SFSOPTEND:COMPANY_ID <> SFPNAME:COMPANY_ID Or
            SFSOPTEND:STATE <> L_STATE Or
            SFSOPTEND:LINE_OF_BUSINESS <> l_line_of_business Or
            SFSOPTEND:CODE <> L_CODE

    If SFPNAME:EFF_DATE >= SFSOPTEND:EFF_DATE Then
        Begin
        L_FORM_EDITION = SFSOPTEND:FORM_EDITION
        L_DESCRIPTION = SFSOPTEND:DESCRIPTION
        End

    Next SFSOPTEND
    End

Add SFPEND
    Begin
    SFPEND:POLICY_NO            =       G_POLICY_NO
    SFPEND:POL_YEAR             =       G_YEAR
    SFPEND:END_SEQUENCE         =       G_END_SEQUENCE
    SFPEND:PREM_NO              =       G_PREM_NO
    SFPEND:BUILD_NO             =       G_BUILD_NO
    SFPEND:SUB_CODE             =       L_SUB_CODE
    SFPEND:CODE                 =       L_CODE
    SFPEND:FORM_EDITION         =       L_FORM_EDITION
    SFPEND:DESCRIPTION          =       L_DESCRIPTION
    SFPEND:PREMIUM              =       0
    SFPEND:LOB_END_CODE         =       "B"
    End

End

End
