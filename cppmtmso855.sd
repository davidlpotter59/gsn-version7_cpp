%%              
Screen
        Name            = "cppmtmso855" ;
        Type            = "Detail" ;
        Padcharacter    = Space ;
        Highlite        = Underscore ;
        Screensize      = "dynamic" ;
        Windowtitle = "%a     %s Endorsement Information     Ver 7.00 %m" ;
        Domain          = "cppENDORSE" ;
        iconline        = 0;

        Global Unsigned Ascii Number G_policy_NO[9] ;
        Global Unsigned Ascii Number g_year[4] ;
        Global Unsigned Ascii Number g_end_sequence[4] ;
        Global Unsigned Ascii Number G_PREM_NO[4] ;
        Global Unsigned Ascii Number G_BUILD_NO[4] ;
        Global String G_CODE[7] ;
        global unsigned ascii number g_sub_code[4] ;
        global unsigned ascii number g_system_rate[1] ;

        global signed ascii number g_premium[9]=0/dec=2 ;

        Local Unsigned Ascii Number L_SUB_CODE[4] ;

        Access cppENDORSE,
           Set cppENDORSE:policy_NO = G_policy_NO ,
               cppendorse:pol_year = g_year,
               cppendorse:end_sequence = g_end_sequence,
               cppENDORSE:PREM_NO = G_PREM_NO ,
               cppENDORSE:BUILD_NO = G_BUILD_NO ,
               cppENDORSE:CODE = G_CODE ;

        Include "STDKEYS7.INC"

style definition

        include "styles.inc"

Functions

        Include "STDFUNC7.INC"

menu definition

        include "stdmenu7.inc"
}

toolbar definition

        include "stdtoolbar7.inc"

}

screen entry
access sfsline, set sfsline:company_id = sfpname:company_id,
                    sfsline:line_of_business = sfpname:line_of_business,
                    sfsline:lob_subline = "00", generic

access validation
if cppendorse:policy_no <> g_policy_no or
   cppendorse:pol_year <> g_year or
   cppendorse:end_sequence <> g_end_sequence or
   cppendorse:prem_no <> g_prem_no or
   cppendorse:build_no <> g_build_no or
   cppendorse:code <> g_code then
    error 1000 ;

screen at 1,1 to 20,80
Properties
        {
        Layouttype = column
        }
{
Panel
Properties
        {
        Borderstyle     = raised
        Scrollbarpolicy = asneeded
        }
{
%%
                                                        _______________
*------------------------------------------------------------------------------*
| Name:  __________________________________________________                    |
|        __________________________________________________                    |
|        __________________________________________________                    |
|        __________________________________________________                    |
|        __________________________________________________                    |
|        __________________________________________________                    |
|        __________________________________________________                    |
*------------------------------------------------------------------------------*
| Premium:    _____________                                                    |
*------------------------------------------------------------------------------*

%%

components

Fields
101     FUNCTION/noecho/showasguidetext ;
301     Begin
        If FUNCTION = "ADD" Then
            Begin
            cppENDORSE:policy_NO = G_policy_NO
            cppendorse:pol_year = g_year
            cppendorse:end_sequence = g_end_sequence
            cppENDORSE:PREM_NO = G_PREM_NO
            cppENDORSE:BUILD_NO = G_BUILD_NO
            cppENDORSE:CODE = G_CODE
            do subcode
            cppENDORSE:SUB_CODE = l_sub_code + 10
            cppendorse:rating_line_of_business = sfsline:rating_line_of_business
            End
        End
        cppENDORSE:NAME[1] ;
401     cppENDORSE:NAME[2]
        if cppendorse:name[2] = "" then
            cppendorse:name[3] = "" ;
501     cppENDORSE:NAME[3]
        Hide If cppENDORSE:NAME[2] = "" ;
601     cppendorse:address1[1]
        hide if cppendorse:name[3] = "" ;
701     cppendorse:address1[2]
        hide if cppendorse:address1[1] = "" ;
801     cppendorse:address2[1]
        hide if cppendorse:address1[2] = "" ;
901     cppendorse:address2[2]
        hide if cppendorse:address2[1] = "" ;

1101    cppendorse:total_premium
        if cppendorse:code = "MCL325" and
           cppendorse:total_premium = 0 then
            error "You must enter a premium" ;

}

CardDialog GUI at 1,80

{
Panel
Properties
        {
        Scrollbarpolicy = asneeded
        Text            = "State"
        }
{
%%
*------------------------------------------------------------------------------*
| State Name: __________________________________________________               |
| Political Subdivision:                                                       |
| ___________________________________________________________________________  |
*------------------------------------------------------------------------------*

%%

components

fields
201     cppENDORSE:STATE_NAME;
401     cppENDORSE:POLITICAL_SUBDIVISION;

} -- end of state


Panel
Properties
        {
        Scrollbarpolicy = asneeded
        Text            = "Description"
        }
{
%%
*------------------------------------------------------------------------------*
| Description: __________________________________________________              |
|                                                                              |
|                                                                              |
|                                                                              |
|                                                                              |
|                                                                              |
|                                                                              |
|                                                                              |
|                                                                              |
|                                                                              |
| Location:                                                                    |
| ___________________________________________________________________________  |
| ___________________________________________________________________________  |
*------------------------------------------------------------------------------*

%%

components

fields
201    cppENDORSE:DESCRIPTION/freetextlines=10;
1301    cppENDORSE:LOCATION[1]/uppercase;
1401    cppENDORSE:LOCATION[2]/uppercase ;

} -- end of desc

}

}

update
if updateok = "YES" and
   abort = "NO" then
    begin
    access sfpend, set sfpend:policy_no    = g_policy_no,
                       sfpend:pol_Year     = g_year,
                       sfpend:end_sequence = g_end_sequence,
                       sfpend:prem_no      = g_prem_no,
                       sfpend:build_no     = g_build_no, generic

    while sfpend:policy_no    = g_policy_no and
          sfpend:pol_Year     = g_year and
          sfpend:end_sequence = g_end_sequence and
          sfpend:prem_no      = g_prem_no and
          sfpend:build_no     = g_build_no
        begin
        if sfpend:code = g_code then
            begin
            change sfpend
                begin
                sfpend:manual_premium = cppendorse:total_premium
                end
            end

        next sfpend
        end

    g_premium = cppendorse:total_premium
    end


procedure definition

procedure subcode
begin
access cppendorse_alias, set cppendorse_alias:policy_no = g_policy_no,
                             cppendorse_alias:pol_year = g_year,
                             cppendorse_alias:end_sequence = g_End_sequence,
                             cppendorse_alias:prem_no = g_prem_no,
                             cppendorse_alias:build_no = g_build_no,
                             cppendorse_alias:code = g_code, generic

l_sub_code = 0
while cppendorse_alias:policy_no = g_policy_no and
      cppendorse_alias:pol_year = g_year and
      cppendorse_alias:end_sequence = g_End_sequence and
      cppendorse_alias:prem_no = g_prem_no and
      cppendorse_alias:build_no = g_build_no and
      cppendorse_alias:code = g_code
        begin
        l_sub_code = cppendorse_alias:sub_code

        next cppendorse_alias
        end

end

End
